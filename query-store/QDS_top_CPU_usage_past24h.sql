/*
This script retrieves the top 15 high‑CPU queries captured by SQL Server Query Store over the past 24 hours. 
It aggregates runtime statistics, identifies the most CPU‑intensive query_ids and plan_ids, and returns key performance details to support troubleshooting and regression analysis. 
The time window can be easily adjusted by modifying the DATEADD parameters.
*/

WITH AggregatedCPU AS
  (SELECT
    q.[query_hash]
   ,SUM(rs.[count_executions] * rs.[avg_cpu_time] / 1000.0) AS "total_cpu_ms"
   ,SUM(rs.[count_executions] * rs.[avg_cpu_time] / 1000.0)/ SUM(count_executions) AS "avg_cpu_ms"
   ,MAX(rs.[max_cpu_time] / 1000.00) AS "max_cpu_ms"
   ,MAX(rs.[max_logical_io_reads]) AS "max_logical_reads"
   ,COUNT(DISTINCT p.[plan_id]) AS "number_of_distinct_plans"
   ,COUNT(DISTINCT p.[query_id]) AS "number_of_distinct_query_ids"
   ,SUM(CASE WHEN rs.[execution_type_desc]='Aborted' THEN rs.[count_executions] ELSE 0 END) AS "aborted_execution_count"
   ,SUM(CASE WHEN rs.[execution_type_desc]='Regular' THEN rs.[count_executions] ELSE 0 END) AS "regular_execution_count"
   ,SUM(CASE WHEN rs.[execution_type_desc]='Exception' THEN rs.[count_executions] ELSE 0 END) AS "exception_execution_count"
   ,SUM(rs.[count_executions]) AS "total_executions"
   ,MIN(qt.[query_sql_text]) AS "sampled_query_text"
   FROM sys.query_store_query_text AS qt
   JOIN sys.query_store_query AS q ON (qt.query_text_id = q.query_text_id)
   JOIN sys.query_store_plan AS p ON (q.query_id = p.query_id)
   JOIN sys.query_store_runtime_stats AS rs ON (rs.plan_id = p.plan_id)
   JOIN sys.query_store_runtime_stats_interval AS rsi ON (rsi.runtime_stats_interval_id = rs.runtime_stats_interval_id)
   WHERE
    rs.[execution_type_desc] IN ('Regular', 'Aborted', 'Exception') AND
    rsi.[start_time]>=DATEADD(HOUR, -24, GETUTCDATE())
   GROUP BY q.[query_hash]),
OrderedCPU AS
   (SELECT *,
    ROW_NUMBER() OVER (ORDER BY [total_cpu_ms] DESC, [query_hash] ASC) AS RN
    FROM AggregatedCPU)
SELECT *
FROM OrderedCPU AS OD
WHERE OD.RN<=15
ORDER BY [total_cpu_ms] DESC;
GO